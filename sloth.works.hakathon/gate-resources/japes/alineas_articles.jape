Phase:	ArticlePrefix
Input:  Lookup Token DottedNumber
Options: control = appelt

Rule:ArticlePrefix
Priority: 10

(
 ({Lookup.majorType=="article"}
 {DottedNumber})
):article
-->
 :article.ArticlePrefix = {rule = "ArticlePrefix"},
 {
     AnnotationSet tokens = (AnnotationSet) bindings.get("article");
     AnnotationSet dottedNumber = tokens.get("DottedNumber", tokens.firstNode().getOffset(), tokens.lastNode().getOffset());
     inputAS.removeAll(dottedNumber);
 }


Phase:	AlineaPrefix
Input:  Token Lookup
Options: control = appelt

Rule: AlineaPrefix
Priority: 10

({Token}):alineaEnd
(
    {Token.string=="("} {Token.kind==number} {Token.string==")"}
):alinea
-->
 :alinea.AlineaPrefix = {rule = "AlineaPrefix"},
 :alineaEnd.AlineaEnd = {rule = "AlineaEnd"}


Phase:	AlineaPrefix2
Input:  DottedNumber Token
Options: control = appelt

Rule: AlineaPrefix2
Priority: 10

(
 ({Token}):alineaEnd
 ({DottedNumber}):ap
)
-->
 :ap.AlineaPrefix = {rule = "AlineaPrefix2"},
 {
     AnnotationSet tokens = (AnnotationSet) bindings.get("ap");
     AnnotationSet dottedNumber = tokens.get("DottedNumber", tokens.firstNode().getOffset(), tokens.lastNode().getOffset());
     inputAS.removeAll(dottedNumber);
 },
 :alineaEnd.AlineaEnd = {rule = "AlineaEnd"}


Phase:	AlineaContent
Input:  AlineaPrefix AlineaEnd DocEnd
Options: control = appelt

Rule:AlineaContent
Priority: 10

(
 {AlineaPrefix}
 ({AlineaEnd} | {DocEnd})
):alinea
-->
 :alinea.AlineaContent = {rule = "AlineaContent"}


Phase:	ArticleEnd
Input:  ArticlePrefix Token
Options: control = appelt

Rule:ArticleEnd
Priority: 10

(
 ({ Token }):articleEnd
 { ArticlePrefix }
)
-->
 :articleEnd.ArticleEnd = {rule = "ArticleEnd"}


Phase:	ArticleText
Input:  ArticlePrefix Token AlineaPrefix DocEnd
Options: control = first

Rule:ArticleText
Priority: 10

(
 (
 { ArticlePrefix }
 ({ Token })+
 ):article
 ({ AlineaPrefix } | { ArticlePrefix} | {DocEnd})
)
-->
 :article.ArticleText = {rule = "ArticleText"}


 Phase:	ArticleContent
 Input:  ArticlePrefix AlineaContent ArticleEnd DocEnd
 Options: control = first

 Rule:ArticleContent
 Priority: 10

 (
  ({ ArticlePrefix }
  ({ AlineaContent })+
  ):article
  ({ArticleEnd} | {DocEnd})
 )
 -->
  :article.ArticleContent = {rule = "ArticleContent"}


 Phase: NumberedChapter
 Input: Chapter Counting Token
 Options: control = first

 Rule: NumberedChapter
 Priority: 10
 (
    ({Chapter}
     {Counting})
     |
     ({Counting}
     {Chapter})
 ):nc
 -->
 :nc.NumberedChapter = {rule = "NumberedChapter" }